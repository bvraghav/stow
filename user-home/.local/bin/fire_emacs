#! /bin/zsh -l

CONDA_ENV=emacs
PARSER_DEF+="nf=OPT_NO_FRAME -no-frame=OPT_NO_FRAME "
PARSER_DEF+="wd=OPT_WITH_DAEMON -with-daemon=OPT_WITH_DAEMON "
PARSER_DEF+="s:=OPT_SOCKET -socket:=OPT_SOCKET "

err () {
  echo ${=@} >&2
}

parse-opts () {
  err "[INFO    ] Using Parser Def: ${PARSER_DEF}"
  err "[INFO    ] Using args: ${@}"

  parse-opts-base $@

  # Set default socket
  OPT_SOCKET=${OPT_SOCKET[2]:-$(get-or-create-socket)}
  err "[INFO    ] Using socket: ${OPT_SOCKET}"

}

parse-opts-base () {
  zmodload zsh/zutil
  zparseopts -D -E -F - ${=PARSER_DEF} || exit 1

  # remove first -- or -
  end_opts=$@[(i)(--|-)]
  set -- "${@[0,end_opts-1]}" "${@[end_opts+1,-1]}"
}

get-or-create-socket () {
  _mktemp () {
    mktemp -p /tmp/emacs$(id -u)
  }

  # err "[INFO    ] Defined _mktemp:" 
  # which _mktemp >&2

  [ -S `realpath ~/.emacs.d/socket` ] || {
    err -n "[DEBUG   ] Removing socket ~/.emacs.d/socket"
    rm ~/.emacs.d/socket
    err " ...done"

    err -n "[DEBUG   ] _mktemp: "                     \
        "Creating with parent: /tmp/emacs$(id -u)"

    ln -s $(_mktemp) ~/.emacs.d/socket

    [ -S `realpath ~/.emacs.d/socket` ]                \
      && err " ...done"                                \
        || err " ...FAILED"

  } && {
    ls ~/.emacs.d/socket
  }
}

is-daemon () {

  local SOCKET=${OPT_SOCKET}
  local SUCCESS="[INFO    ] Checking for socket:${SOCKET} ...works"
  local FAILURE="[WARNING ] Checking for socket:${SOCKET} ...FAILED"

  local SEXP="(message \"Hello\")"
  # err "[DEBUG   ] SEXP:${SEXP}"
  # err "[INFO    ] Checking for socket:${SOCKET}"
  emacsclient -s ${SOCKET} -n -e ${SEXP} >/dev/null && {
    err ${SUCCESS}
  } || {
    err ${FAILURE}
    return 1
  }

}

conda-activate () {
  source ~/miniconda3/bin/activate ${CONDA_ENV}
}

run-daemon-may-be () {
  [ ! -z ${OPT_WITH_DAEMON} ] && {
    local SOCKET=${OPT_SOCKET}

    err "[INFO    ] Using socket: ${SOCKET}"
    conda-activate && {
      
      export NVM_DIR="$HOME/.nvm"
      [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm

      emacs --daemon=${SOCKET}
    }
  }
}

create-frame-may-be () {
  local SOCKET=${OPT_SOCKET}

  # should be a valid exit if [ ! -z ${OPT_NO_FRAME} ]
  [ ! -z ${OPT_NO_FRAME} ] || {

    emacsclient -s ${SOCKET} -c ${@}
  }
}

main() {

  parse-opts $@

  { is-daemon || run-daemon-may-be }                     \
    && create-frame-may-be $@

}

main $@
